#!/usr/bin/env zsh
# vim: ft=zsh sw=2 ts=2 et

function prompt_nicoulaj_setup() {
  setopt noxtrace noksharrays localoptions
  prompt_opts=(cr subst percent)

  # Load required modules.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd vcs_info

  # vcs_info setup.
  zstyle ':vcs_info:*' enable hg bzr svn git
  zstyle ':vcs_info:*' check-for-changes true # Can be slow on big repos.
  zstyle ':vcs_info:*' unstagedstr " ●"
  zstyle ':vcs_info:*' actionformats "%S" "%r/%s/%b (%a)" "%u"
  zstyle ':vcs_info:*' formats "%S" "%r/%s/%b" "%u"
  zstyle ':vcs_info:*' nvcsformats "%~" "" ""
  
  # Prompts definition.
  PROMPT="%(0?.%{${FG[71]}%}.%{${FG[124]}%})${SSH_TTY:+[%n@%m]}%{$FX[bold]%}%30<..<"'${vcs_info_msg_0_%%.}'"%<<%(!.❯❯❯.❯)%{$FX[no-bold]%}%{$FX[reset]%} "
  RPROMPT="%{${FG[darkgrey]}%}"'$vcs_info_msg_1_'"%{$FX[reset]%}%{${FG[172]}%}"'$vcs_info_msg_2_'"%{$FX[reset]%}"
}

prompt_nicoulaj_setup "$@"
